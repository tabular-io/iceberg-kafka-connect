plugins {
  id "distribution"
}

configurations.all {
  resolutionStrategy.force "net.minidev:json-smart:2.4.11"
  resolutionStrategy.force "org.codehaus.jettison:jettison:1.5.4"
}

dependencies {
  implementation project(":iceberg-kafka-connect")
  implementation libs.bundles.iceberg.ext
  implementation libs.bundles.aws
  implementation(libs.hadoop.common) {
    exclude group: "log4j"
    exclude group: "org.slf4j"
    exclude group: "ch.qos.reload4j"
  }

  testImplementation libs.bundles.iceberg
  testImplementation libs.bundles.aws
  testImplementation libs.bundles.jackson
  testImplementation libs.bundles.kafka.connect

  testImplementation libs.junit.api
  testRuntimeOnly libs.junit.engine

  testImplementation libs.mockito
  testImplementation libs.assertj
  testImplementation libs.awaitility
  testImplementation libs.testcontainers
  testImplementation libs.testcontainers.kafka
  testImplementation libs.http.client
}

processResources {
  filter {
    it.replace('__VERSION__', project.version)
  }
}

distributions {
  main {
    contents {
      into("lib/") {
        from configurations.runtimeClasspath
      }
      into("doc/") {
        from "$rootDir/LICENSE"
        from "$rootDir/README.md"
      }
      from(processResources.destinationDir) {
        include "manifest.json"
      }
    }
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifact distZip
    }
  }
}

tasks.distTar.enabled = false
tasks.jar.enabled = false

installDist.dependsOn processResources
distZip.dependsOn processResources

// build the install before test so it can be installed into kafka connect
test.dependsOn installDist

assemble.dependsOn distZip
